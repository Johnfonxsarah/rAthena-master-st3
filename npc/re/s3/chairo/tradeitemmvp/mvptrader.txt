//===== Current Version: =====================================
//= 1.1 
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Exchange cards for points?.?
//============================================================

morocc,163,88,3	script	mvp Trader	4_M_ZONDAOYAJI,{
	mes "[Card Trader]";
	mes "หวัดดี, "+strcharinfo(0)+"!";
	mes "มีอะไรให้ข้ารับใช้ไหม ?";
	next;
	switch(select(" > รายละเอียด: > แลกการ์ดเป็นแต้มพอยท์: > ร้านค้าใช้พอยท์ซื้อ (^0055FF"+getd(.Points$)+"^000000): > ออก")) {
	case 1:
		mes "[Item Trader]";
		mes "เจ้าเคยมีปัญหาเวลาได้";
		mes "การ์ดที่ไม่ได้ใช้ไหม ?";
		mes "ปัญหานั้นจะหมดไป";
		mes "เพียงนำมันมาให้ข้า!";
		next;
		mes "[Item Trader]";
		mes "ข้าจะให้เจ้า ^0055FF"+.Points[0]+" แต้ม"+((.Points[0] == 1)?"":"")+"^000000 ต่อใบ";
		mes "สำหรับการ์ดทั่วไป";
		mes "เจ้าสามารถเอาแต้มพอยท์";
		mes "ไปซื้อของในร้านค้าของข้าได้.";
		mes "ฟังดูน่าสนใจไหม ?";
		close;
	case 2:
		mes "[Card Trader]";
		mes "กรุณาเลือกการ์ด";
		mes "ที่เจ้าต้องการแลก.";
		if (.Level) {
			mes " ";
			mes "ข้าต้องการจาก";
			mes "มอนสเตอร์ที่เลเวล";
			mes .Level+" หรือมากกว่า.";
		}
		deletearray @sold_nameid[0],getarraysize(@sold_nameid);
		callshop "item_shopp",2;
		npcshopattach "item_shopp";
		end;
	case 3:
		mes "[Card Trader]";
		mes "เจ้ามี ^0055FF"+getd(.Points$)+"^000000 แต้ม"+((getd(.Points$) == 1)?".":".");
		callshop "item_shopp",1;
		npcshopattach "item_shopp";
		end;
	case 4:
		mes "[Item Trader]";
		mes "*ง่าส์*";
		mes "แล้วมาใหม่น้า!";
		close;		
	}

OnSellItem:
	mes "การ์ดที่จะแลก:";
	mes "-----------------------------------";
	for(set .@i,0; .@i<getarraysize(@sold_nameid); set .@i,.@i+1)
		if (@sold_nameid[.@i] > 4000 && @sold_nameid[.@i] < 4700) {
			if (.Level) {
				query_sql("SELECT `LV` FROM `mob_db` WHERE `DropItemid` = "+@sold_nameid[.@i],.@lv);
				if (.@lv < .Level) {
					dispbottom getitemname(@sold_nameid[.@i])+" is under the minimum level.";
					continue;
				}
			}
			set .@Item_id[getarraysize(.@Item_id)], @sold_nameid[.@i];
			set .@Item_amt[getarraysize(.@Item_amt)], @sold_quantity[.@i];
			set .@item, compare(.ITEM$,""+@sold_nameid[.@i]);
			mes ((.@item)?"  ^FF0000":"  ^777777")+@sold_quantity[.@i]+"x "+getitemname(@sold_nameid[.@i])+"^000000";
			set .@Item_total, .@Item_total+(@sold_quantity[.@i]*((.@item)?.Points[1]:.Points[0]));
		}
	deletearray @sold_nameid[0], getarraysize(@sold_nameid);
	deletearray @sold_quantity[0], getarraysize(@sold_quantity);
	if (!.@Item_id) {
		mes "^777777(ไม่มีการ์ด)^000000";
		close;
	}
	mes " ";
	mes "---------- รวมทั้งหมด: ^0055FF"+.@Item_total+" แต้ม.^000000 -------";
	next;
	if(select(" > ^0055FFแลกเปลี่ยน...^000000: > ^777777ยกเลิก^000000") == 2) {
		mes "[Card Trader]";
		mes "โอ้, ได้เลย...";
		close;
	}
	for(set .@i,0; .@i<getarraysize(.@Item_id); set .@i,.@i+1)
		delitem .@Item_id[.@i],.@Item_amt[.@i];
	setd .Points$, getd(.Points$)+.@Item_total;
	mes "[Item Trader]";
	mes "เรียบร้อย!";
	close;

OnBuyItem:
	for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)
		for(set .@j,0; .@j<getarraysize(.Shop); set .@j,.@j+2)
			if (@bought_nameid[.@i] == .Shop[.@j]) {
				set .@cost, .@cost+(.Shop[.@j+1]*@bought_quantity[.@i]);
				break;
			}
	if (.@cost > getd(.Points$)) {
		mes "[Card Trader]";
		mes "เจ้ามีแต้มพอยท์ไม่พอ.";
	}
	else {
		mes "รายการสินค้า:";
		mes "-----------------------------------";
		for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1) {
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
			mes "  ^777777"+@bought_quantity[.@i]+"x "+getitemname(@bought_nameid[.@i])+"^000000";
		}
		mes " ";
		mes "---------- รวมทั้งหมด: ^0055FF"+.@cost+" แต้ม.^000000 -------";
		setd .Points$, getd(.Points$)-.@cost;
	}
	deletearray @bought_nameid[0], getarraysize(@bought_nameid);
	deletearray @bought_quantity[0], getarraysize(@bought_quantity);
	close;

OnInit:
	set .Level,0;   		// เลเวลมอนสเตอร์ขั้นต่ำในการแลกการ์ด.
	set .Points$,"#Item_Points";	// ตัวแปรพอยท์.
	setarray .Shop[0],		// ร้านขายของ: <รหัสไอเท็ม>,<ราคาพอยท์>
	1200008,2,616,30,12246,500,14003,50;
	setarray .Points[0],0,1,1;	// แต้มพอยท์ต่อ <การ์ดทั่วไป>,<การ์ดบอส>
	set .ITEM$,			// รหัสการ์ดบอส.
	  "2504,2311"+
	  "2403,2103";

	npcshopdelitem "item_shopp",909;
	for(set .@i,0; .@i<getarraysize(.Shop); set .@i,.@i+2)
		npcshopadditem "item_shopp",.Shop[.@i],.Shop[.@i+1];
	waitingroom "Card แลกแต้ม",0;
	end;
}
-	shop	item_shopp	-1,909:-1