//===== rAthena Script ======================================= 
//= Orc's Memory
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Relive the memory of an orc.
//===== Additional Comments: ================================= 
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 First round of bugfixes. (bugreport:3928) [L0ne_W0lf]
//=     Fixed some npcs calling wrong events. 
//=     Fixed Depraved Orc Spirit spawn point
//=     Fixed an NPC that was never being enabled.
//= 1.1a Second round from bugreport. (bugreport:3928) [L0ne_W0lf]
//=     Fixed first warp never enabling, issue not seen
//=     because of a local source mod.
//=     Corrected the IDs for the Vengeful Orc Spirit and
//=     Shaman Cargalache, copy/paste fail.
//=     Changed spawn point for dungeon according to spawn
//=     point on iRO.
//= 1.2 More bugfixes, mostly typos. :O (bugreport:3944) [L0ne_W0lf]
//=     Fixed some varaibles (types/values) as well.
//= 1.3 Disabled an exploit related to the orc hero spawn. [L0ne_W0lf]
//=     Corrected the variable type for party leader name.
//= 1.4 Fixed donpcevent calling non-existant npc. (bugreport:4039) [L0ne_W0lf]
//= 1.5 Fixed a checkquest condition never setting quest. [L0ne_W0lf]
//= 1.6 Added 'instance_check_party' command to the script for proper checking if
//=     the invoking party meets the requirements to join the Memorial Dungeon.
//= 1.7 Instance system rewrite. [Euphy]
//============================================================ 

// Entrance
//============================================================
gef_fild10,242,202,0	script	Dimensional Gorge Piece	406,{

	set .@party_id,getcharid(1);
	set .@md_name$,"Orc's Memory";

	if (!instance_check_party(.@party_id,2,30,80)) {
		mes "เฉพาะผู้ที่เลเวล ^ff000030 ~ 99^000000 ถึงจะสามารถเข้าไปภายในดันเจี้ยนได้.";
		close;
	}

	set .@orctime,checkquest(12059,PLAYTIME);
	if (.@orctime == -1) {
		if (is_party_leader() == true) {
			mes "คุณสร้างปาตี้เรียบร้อยแล้ว. คุณต้องการจะจองคิวเข้า "+.@md_name$+"ไหม ?";
			next;
			switch(select("สร้างดันเจี้ยน "+.@md_name$+":เข้าสู่ดันเจี้ยน:ยกเลิก")) {
			case 1:
				if (instance_create(.@md_name$) < 0) {
					mes "Party Name: "+ getpartyname(.@party_id);
					mes "Party Leader: "+strcharinfo(0);
					mes "^0000ff"+.@md_name$+" ^000000 - Reservation Failed.";
					close;
				}
				mes "^0000ff"+.@md_name$+"^000000- ได้จองคิวการเข้าใช้แล้ว";
				mes "คุณสามารถเข้าดันเจี้ยนได้โดยเลือก 'เข้าสู่ดันเจี้ยน' โดยคุยกับ NPC อีกครั้งหลังจองคิวเรียบร้อยแล้ว";
				close;
			case 2:
				callsub L_Enter,0;
			case 3:
				close;
			}
		}
		if(select(.@md_name$ + " เข้าสู่ดันเจี้ยน:ยกเลิก") == 2)
			end;
		callsub L_Enter,1;
	} else if (.@orctime == 0 || .@orctime == 1) {
		mes "ตอนนี้คุณสามารถเข้าไปภายในดันเจี้ยน ได้แล้วแล้ว.";
		next;
		if(select("เข้าสู่ดันเจี้ยน:ยกเลิก") == 2)
			close;
		callsub L_Enter,0;
	} else if (.@orctime == 2) {
		mes "^0000ffAll บันทึกเวลาที่เกี่ยวกับ Orc's Memory Dungeon จะถูกลบ. ตอนนี้คุณสามารถสร้างใหม่หรือเข้าสู่ดันเจี้ยนอีกครั้ง.^000000";
		erasequest 12059;
		close;
	}
	mes "ถ้าหากคุณต้องการจะสร้างดันเจี้ยน คุณต้องเป็นหัวหน้าปาตี้และมีสมาชิกอย่างน้อย 2 คน.";
	close;
L_Enter:
	switch(instance_enter("Orc's Memory")) {
	case IE_OTHER:
		mes "เกิดข้อผิดพลาดที่ไม่รู้จัก.";
		close;
	case IE_NOINSTANCE:
		mes "เฉพาะสมาชิกปาตี้เท่านั้นที่สามารถสร้าง Memorial Dungeon.";
		close;
	case IE_NOMEMBER:
		mes "Only a member of the party can enter the Memorial Dungeon.";
		close;
	case IE_OK:
		mapannounce "gef_fild10", getpartyname( getcharid(1) ) +" party's member "+strcharinfo(0)+" has entered the Orc's Memory.",bc_map,"0x00ff99";
		if (checkquest(12059) == -1) setquest 12059;
		//warp "1@orcs",179,15;
		if (getarg(0) == 0) close;
		else end;
	}

}

gef_fild10,238,202,6	script	Mad Scientist#orc	865,{
	if (BaseLevel < 50) {
		mes "นักวิทยาศาสตร์สติเฟื่องดูเหมือนจะไม่สังเกตเห็นคุณและพึมพํากับตัวเอง...";
		close;
	}
	if (mad == 0) {
		mes "[นักวิทยาศาสตร์สติเฟื่อง]";
		mes "ฮาฮ่า, ไม่มีอ็อคตัวไหนเข้ามาใกล้ฉันได้!";
		mes "ด้วยพลังแห่ง Dimensional Gorge ช่างแข็งแกร่งจริงๆ!!";
		next;
		switch(select("พวกอ็อคไม่โจมตีคุณยังงั้นหรอ?:คิดไปเองรึป่าว!")) {
		case 1:
			mes "[นักวิทยาศาสตร์สติเฟื่อง]";
			mes "ใช่, ใช่.. ฉันรู้มันยากที่จะเชื่อ. แต่ฉันไม่ว่าคุณหรอก.";
			mes "ฉันศึกษา the Dimensional Gorge ที่พบแถวๆ เมือง Morocc.";
			mes "จากการวิจัยของฉัน, แร่ธาตุพวกนี้มีพลังมหาศาลอยู่ด้านใน!";
			next;
			break;
		case 2:
			mes "[นักวิทยาศาสตร์สติเฟื่อง]";
			mes "ดูเหมือนว่าคุณจะไม่เชื่อสินะ, หื้ม?";
			next;
			break;
		}
		select("โอ้? ยังงั้นหรอ, เดินทางข้ามเวลาสินะ?");
		mes "[นักวิทยาศาสตร์สติเฟื่อง]";
		mes "ฉลาดมาก! ฉันก็พึ่งจะทำแบบนั้นไปเมื่อกี้นี้เอง! เห็นเจ้ารูปปั้นตรงนั้นไหม? เจ้านั่นมันไม่ใช่รูปปั้นธรรมดาหรอกนะ!";
		next;
		mes "[นักวิทยาศาสตร์สติเฟื่อง]";
		mes "ฉันสร้างมันด้วยการผสมผสานระหว่างเทคโนโลยี และ พลังงานอันไร้ขีดจำกัด ฉันไปเจอมันในช่องเขาไงล่ะ! นายอยากจะลองใช้มันไหม?";
		next;
		select("...อืม, มันดูไม่ใช่ตัวเลือกที่ดีสักเท่าไร?");
		mes "[นักวิทยาศาสตร์สติเฟื่อง]";
		mes "เฮ้, ตั้งแต่เมื่อไรที่เจ้าสิ่งนี้มันดูไม่ใช่ตัวเลือกที่ไม่ดี!? แล้วมันยังไงล่ะ! คุณสามารถพูดคุยกับอ็อคได้เลยนะ! มาเถอะหน่า!";
		next;
		mes "[นักวิทยาศาสตร์สติเฟื่อง]";
		mes "และเป็นไปได้ว่าคุณจะได้รับคำแนะนำจากเขามากมายเลย เมื่อพวกเขาเห็นคุณ, ดังนั้นเพียงแค่เข้าไป! พวกเขาจะพูดเหมือนเดิมทุกๆครั้ง, จนถึงจุดที่ ฉันจำพวกเขาได้!";
		next;
		switch(select("เอาละ... แล้วฉันต้องทำยังไงบ้าง?:อืมม, ไม่ดีกว่า.")) {
		case 1:
			mes "[นักวิทยาศาสตร์สติเฟื่อง]";
			mes "เยี่ยมมาก! เพียงแค่คุณวางมือบนลงรูปปั้นนั่น! และทำตัวอย่างเป็นธรรมชาติ เมื่อคุณเข้าไปในนั้น!";
			set mad,1;
			close;
		case 2:
			mes "[นักวิทยาศาสตร์สติเฟื่อง]";
			mes "โอ้ โอเค, แล้วแต่คุณเลือกเลย!";
			close;
		}
	} else if (mad == 1) {
		mes "[นักวิทยาศาสตร์สติเฟื่อง]";
		mes "หื้่มม? วางมือของคุณบนรูปปั้นนั่น ถ้าคุณอยากจะได้ประสบการณ์การเดินทางข้ามมิติ!";
		close;
	} else {
		set mad,1;
		end;
	}
}

// Orc's Memory Floor 1
//============================================================
1@orcs,180,30,0	script	#Resurrect Monsters1	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#Resurrect Monsters1");
	end;

OnDisable:
	stopnpctimer;
	killmonster instance_mapname("1@orcs"),instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
	end;

OnEnable:
	enablenpc instance_npcname("#Resurrect Monsters1");
	monster instance_mapname("1@orcs"),0,0,"Orc Warrior",1023,30,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("1@orcs");
	set .@mob_dead_num, 30 - mobcount(.@map$,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead");
	if (.@mob_dead_num > 0) {
		set .@mob_ran,rand(1,30);
		if (.@mob_ran > 29) {
			monster .@map$,0,0,"Orc Warrior",1023,.@mob_dead_num,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
		}
		else if ((.@mob_ran > 28) && (.@mob_ran < 30)) {
			monster .@map$,0,0,"High Orc",1213,.@mob_dead_num,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
			if (rand(1,10) == 9) {
				mapannounce .@map$, "High Orc: We need more defenses! Get more people here!",bc_map,"0xff4444";
			}
		}
		else if ((.@mob_ran > 26) && (.@mob_ran < 29)) {
			areamonster .@map$,41,91,51,81,"High Orc",1213,.@mob_dead_num,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
			if (rand(1,10) == 9) {
				mapannounce .@map$, "Where are the High Orcs!? Get them to stop the enemies!",bc_map,"0xff4444";
			}
		}
		else {
			areamonster .@map$,17,187,27,177,"High Orc",1213,.@mob_dead_num,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
			if (rand(1,5) == 3) {
				mapannounce .@map$, "Caution: The army's starting to concentrate at Zone No. 4.",bc_map,"0x77ff77";
			}
			if (rand(1,100) == 50) {
				initnpctimer;
			}
		}
	}
	end;

OnTimer10:
	mapannounce instance_mapname("1@orcs"), "Shouts of the Chief Orc of Safeguards: Looks like this will take longer than expected. Summon the Stalactic Golems!",bc_map,"0xff4444";
	end;

OnTimer4010:
	set .@map$, instance_mapname("1@orcs");
	mapannounce .@map$, "Stalactic Golems are digging out of the deep underground.",bc_map,"0x77ff77";
	areamonster .@map$,17,187,27,177,"Stalactic Golem",1278,20,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
	stopnpctimer;
	end;
}

1@orcs,180,30,0	script	#Resurrect Monsters2	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#Resurrect Monsters2");
	end;

OnDisable:
	killmonster instance_mapname("1@orcs"),instance_npcname("#Resurrect Monsters2")+"::OnMyMobDead";
	end;

OnEnable:
	enablenpc instance_npcname("#Resurrect Monsters2");
	monster instance_mapname("1@orcs"),0,0,"Trained Wolf",1106,15,instance_npcname("#Resurrect Monsters2")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("1@orcs");
	set .@mob_dead_num, 15 - mobcount(.@map$,instance_npcname("#Resurrect Monsters2")+"::OnMyMobDead");
	if (rand(1,30) > 15) {
		if (.@mob_dead_num > 0) {
			monster .@map$,0,0,"Trained Wolf",1106,.@mob_dead_num,instance_npcname("#Resurrect Monsters2")+"::OnMyMobDead";
		}
	}
	else {
		if (.@mob_dead_num > 0) {
			areamonster .@map$,17,187,27,177,"Trained Wolf",1106,.@mob_dead_num,instance_npcname("#Resurrect Monsters2")+"::OnMyMobDead";
		}
	}
	end;
}

1@orcs,180,30,0	script	#Resurrect Monsters3	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#Resurrect Monsters3");
	end;

OnDisable:
	killmonster instance_mapname("1@orcs"),instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead";
	end;

OnEnable:
	enablenpc instance_npcname("#Resurrect Monsters3");
	monster instance_mapname("1@orcs"),0,0,"Orc Archer",1189,15,instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("1@orcs");
	set .@mob_dead_num, 15 - mobcount(.@map$,instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead");
	set .@mob_ran,rand(1,30);
	if (.@mob_ran > 29) {
		if (.@mob_dead_num > 0) {
			monster .@map$,0,0,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead";
		}
	}
	else if ((.@mob_ran > 26) && (.@mob_ran < 30)) {
		if (.@mob_dead_num > 0) {
			areamonster .@map$,43,155,47,159,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead";
			if (rand(1,3) == 3) {
				mapannounce .@map$, "High Orc: Attack them from behind! Cut off their support!",bc_map,"0xff4444";
			}
		}
	}
	else {
		if (.@mob_dead_num > 0) {
			areamonster .@map$,17,187,27,177,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead";
		}
	}
	end;
}

1@orcs,180,30,0	script	#Resurrect Monsters4	844,{
	end;

OnInstanceInit:
	enablenpc instance_npcname("#Resurrect Monsters4");
	areamonster instance_mapname("1@orcs"),98,35,178,115,"Anopheles",1627,10,instance_npcname("#Resurrect Monsters4")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("1@orcs");
	set .@mob_dead_num, 10 - mobcount(.@map$,instance_npcname("#Resurrect Monsters4")+"::OnMyMobDead");
	if (.@mob_dead_num > 0) {
		monster .@map$,0,0,"Anopheles",1627,.@mob_dead_num,instance_npcname("#Resurrect Monsters4")+"::OnMyMobDead";
	}
	end;

OnDisable:
	killmonster instance_mapname("1@orcs"),instance_npcname("#Resurrect Monsters4")+"::OnMyMobDead";
	end;
}

1@orcs,180,29,4	script	Kruger#1-1	433,{
	donpcevent instance_npcname("Kruger#1-2")+"::OnEnable";
	end;
}

1@orcs,180,29,4	script	Kruger#1-2	433,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("Kruger#1-2");
	end;

OnEnable:
	disablenpc instance_npcname("Kruger#1-1");
	enablenpc instance_npcname("Kruger#1-2");
	initnpctimer;
	end;

OnTimer10:
	mapannounce instance_mapname("1@orcs"), "Kruger: เวรเอ้ย... เกิดปัญหาอะไรขึ้น!! ทำไมใช้เวลานานขนาดนี้เร่งมือเข้า!!",bc_map,"0xffff00";
	end;

OnTimer5710:
	mapannounce instance_mapname("1@orcs"), "Kruger: แผนของข้าคือให้พวกเพื่อนข้าเปิดประตู, แต่ทั้งหมดต้องพังเพราะพวกเจ้าเสียเวลาไปเปล่าประโยชน์และถูกจับขังโดย Orc Shaman.",bc_map,"0xffff00";
	end;

OnTimer14610:
	mapannounce instance_mapname("1@orcs"), "เสียงตะโกนของหัวหน้าอ็อค ที่ดูแลความปลอดภัย: ข้าได้กลิ่นปลาเค็ม..ส่งยามไปดูที่หน้าทางเข้า!!",bc_map,"0xff4444";
	end;

OnTimer20210:
	mapannounce instance_mapname("1@orcs"), "Kruger: เวรเอ้ย.. พวกมันจะมาถึงอยู่แล้ว. เอาละ. ตอนนี้ฟังข้าให้ดี.",bc_map,"0xffff00";
	end;

OnTimer24910:
	mapannounce instance_mapname("1@orcs"), "Kruger: Orc Shaman ได้ผนึกฐานชั้นแรกแล้วโดยแบ่งออกเป็น 4 โซน. แต่ละโซนจะมี 1 Orc Safeguard ผู้ที่มีถูกบรรจุเวทย์สำหรับปลดผนึกแต่ละโซนเอาไว้.",bc_map,"0xffff00";
	end;

OnTimer34310:
	mapannounce instance_mapname("1@orcs"), "Kruger: ค้นหาและกำจัดมันซะ เจ้าจะสามารถไปยัง โซนถัดไปได้เมื่อพลังเวทย์นั้นถูกทำลาย.",bc_map,"0xffff00";
	end;

OnTimer39710:
	mapannounce instance_mapname("1@orcs"), "Kruger: สิ่งหนึ่งที่เจ้าต้องจำเอาไว้คือหลีกเลี่ยงพวก อ็อคที่ไม่ได้มีพลังเวทย์สะกดเอาไว้ เพราะว่าทุกครั้งที่เจ้ากำจัดพวกมัน Orc Assaulters จะมารวมตัวกันชั้นที่ 2.",bc_map,"0xffff00";
	end;

OnTimer49210:
	mapannounce instance_mapname("1@orcs"), "Kruger: ในกรณีที่เลวร้ายที่สุด, ส่วนของชั้น 2 จะถูกปิดตาย. เจ้าจะต้องระวังให้มากที่สุดเพื่อตัวเจ้าเอง.",bc_map,"0xffff00";
	end;

OnTimer56310:
	mapannounce instance_mapname("1@orcs"), "ภารกิจ: ลอบเข้าไปกำจัด 'Orc Sealed by Magical Power' หลีกเลี่ยงการสู้กับอ็อคตัวอื่นๆ ซึ่งอาจจะทำให้เกิดปัญหาชั้นที่สองได้.",bc_map,"0x44ffff";
	donpcevent instance_npcname("#Resurrect Monsters1")+"::OnEnable";
	donpcevent instance_npcname("#Resurrect Monsters2")+"::OnEnable";
	donpcevent instance_npcname("#Resurrect Monsters3")+"::OnEnable";
	disablenpc instance_npcname("Kruger#1-2");
	end;

OnTimer60000:
	areamonster instance_mapname("1@orcs"),137,83,143,89,"Enchanted Orc",1023,1,instance_npcname("B1 Area Mobs")+"::OnMyMobDead";
	stopnpctimer;
	end;

}

1@orcs,168,125,0	script	B1 Area 1	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("B1 Area 1");
	end;

OnEnable:
	enablenpc instance_npcname("B1 Area 1");
	areamonster instance_mapname("1@orcs"),103,105,109,111,"Enchanted Orc",1023,1,instance_npcname("B1 Area Mobs")+"::OnMyMobDead1";
	end;

OnTouch_:
	warp instance_mapname("1@orcs"),168,130;
	end;

OnContinue:
	donpcevent instance_npcname("B1 Area 2")+"::OnEnable";
	initnpctimer;
	end;

OnTimer10300:
	mapannounce instance_mapname("1@orcs"), "เสียงกระซิบของ Kruger: อ็อคที่นี่เคยเป็นลูกน้องของข้าเมื่อไม่กี่เดือนมานี้ พวกเขาเปลี่ยนไปตั้งแต่ Orc Shamans เริ่มที่จะควบคุมพวกเขาด้วยเวทมนต์",bc_map,"0xff4499";
	end;

OnTimer18700:
	mapannounce instance_mapname("1@orcs"), "เสียงกระซิบของ Kruger: ไม่มีอะไรที่เราทำได้นอกจากกำจัด Orc Shaman เพื่อรักษาเผ่าของเราไว้",bc_map,"0xff4499";
	stopnpctimer;
	end;
}

1@orcs,89,94,0	script	B1 Area 2	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("B1 Area 2");
	end;

OnEnable:
	enablenpc instance_npcname("B1 Area 2");
	areamonster instance_mapname("1@orcs"),32,40,38,46,"Enchanted Orc",1023,1,instance_npcname("B1 Area Mobs")+"::OnMyMobDead2";
	end;

OnTouch_:
	warp instance_mapname("1@orcs"),85,85;
	end;

OnContinue:
	donpcevent instance_npcname("B1 Area 3")+"::OnEnable";
	initnpctimer;
	end;

OnTimer30300:
	mapannounce instance_mapname("1@orcs"), "เสียงกระซิบของ Kruger: ข้าเห็นร่างของเผ่าเดียวกัน เหมือนกับว่า Shaman ใช้พวกอ็อคเพื่อทำพิธีกรรมบางอย่าง",bc_map,"0xff4499";
	end;

OnTimer37600:
	mapannounce instance_mapname("1@orcs"), "เสียงกระซิบของ Kruger: ทั้งหมดนี่เป็นเพราะข้า ข้าควรจะรับผิดชอบต่อเหตุการณ์ครั้งนี้",bc_map,"0xff4499";
	stopnpctimer;
	end;
}

1@orcs,38,105,0	script	B1 Area 3	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("B1 Area 3");
	end;

OnEnable:
	enablenpc instance_npcname("B1 Area 3");
	areamonster instance_mapname("1@orcs"),19,177,25,183,"Enchanted Orc",1023,1,instance_npcname("B1 Area Mobs")+"::OnMyMobDead3";
	end;

OnTouch_:
	warp instance_mapname("1@orcs"),38,110;
	end;

OnContinue:
	donpcevent instance_npcname("B1 Area 4")+"::OnEnable";
	initnpctimer;
	end;

OnTimer30300:
	mapannounce instance_mapname("1@orcs"), "ได้โปรด, กรุณารออยู่ในนั้น!",bc_map,"0xff4499";
	end;

OnTimer32700:
	mapannounce instance_mapname("1@orcs"), "เราจะได้พักบ้าง เมื่อเราไปถึงชั้นที่ 2 หลังจากที่ผ่านทางนี้ไป",bc_map,"0xff4499";
	stopnpctimer;
	end;
}

1@orcs,21,189,0	script	B1 Area 4	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("B1 Area 4");
	end;

OnEnable:
	enablenpc instance_npcname("B1 Area 4");
	donpcevent instance_npcname("#Resurrect Monsters1")+"::OnDisable";
	donpcevent instance_npcname("#Resurrect Monsters2")+"::OnDisable";
	donpcevent instance_npcname("#Resurrect Monsters3")+"::OnDisable";
	donpcevent instance_npcname("#Resurrect Monsters4")+"::OnDisable";
	end;

OnTouch_:
	warp instance_mapname("2@orcs"),32,171;
	end;
}

1@orcs,1,1,0	script	B1 Area Mobs	-1,{
	end;

OnMyMobDead:
	donpcevent instance_npcname("B1 Area 1")+"::OnEnable";
	end;

OnMyMobDead1:
	donpcevent instance_npcname("B1 Area 1")+"::OnContinue";
	end;

OnMyMobDead2:
	donpcevent instance_npcname("B1 Area 2")+"::OnContinue";
	end;

OnMyMobDead3:
	donpcevent instance_npcname("B1 Area 3")+"::OnContinue";
	end;
}

// Orc's Memory Floor 2
//============================================================
2@orcs,36,171,0	script	#2Resurrect Monsters1	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#2Resurrect Monsters1");
	end;

OnEnable:
	enablenpc instance_npcname("#2Resurrect Monsters1");
	monster instance_mapname("2@orcs"),0,0,"Vengeful Orc",1152,30,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
	end;

OnDisable:
	stopnpctimer;
	killmonster instance_mapname("2@orcs"),instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("2@orcs");
	set .@mob_dead_num, 30 - mobcount(.@map$,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead");
	if (.@mob_dead_num > 0) {
		set .@mob_ran,rand(1,30);
		if (.@mob_ran > 29) {
			monster .@map$,0,0,"Vengeful Orc",1152,.@mob_dead_num,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
		}
		else if ((.@mob_ran > 28) && (.@mob_ran < 30)) {
			monster .@map$,0,0,"High Orc",1213,.@mob_dead_num,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
		}
		else if ((.@mob_ran > 26) && (.@mob_ran < 29)) {
			areamonster .@map$,157,112,167,122,"High Orc",1213,.@mob_dead_num,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
			if (rand(1,10) == 9) {
				mapannounce .@map$, "คำเตือน: High Orcs กำลังรวมตัวกันใกล้ area 3.",bc_map,"0xff4444";
			}
		}
		else {
			areamonster .@map$,173,13,183,23,"High Orc",1213,.@mob_dead_num,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
			if (rand(1,5) == 3) {
				mapannounce .@map$, "คำเตือน: กองกำลังเริ่มหันไปสนใจที่แท่นบูชาของ Shaman's ",bc_map,"0x77ff77";
			}
			if (rand(1,70) == 50) {
				initnpctimer;
			}
		}
	}
	end;

OnTimer10:
	mapannounce instance_mapname("2@orcs"), "เสียงกระซิบจากที่ไหนสักแห่ง: เจ้าโง่เอ้ย... คิดหรอว่าทำแบบนั้นจะทำให้แท่นบูชาพังได้หน่ะ ?",bc_map,"0xff4444";
	end;

OnTimer4010:
	set .@map$, instance_mapname("2@orcs");
	mapannounce .@map$, "[ Wraiths were summoned by an unknown power ]",bc_map,"0x77ff77";
	areamonster .@map$,167,25,177,35,"Wraith",1475,30,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
	stopnpctimer;
	end;
}

2@orcs,36,171,0	script	#2Resurrect Monsters3	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#2Resurrect Monsters3");
	end;

OnEnable:
	enablenpc instance_npcname("#2Resurrect Monsters3");
	monster instance_mapname("2@orcs"),0,0,"Orc Zombie",1153,15,instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("2@orcs");
	set .@mob_dead_num, 15 - mobcount(.@map$,instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead");
	set .@mob_ran,rand(1,30);
	if (.@mob_ran > 29) {
		if (.@mob_dead_num > 0) {
			monster .@map$,0,0,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead";
		}
	}
	else if ((.@mob_ran > 6) && (.@mob_ran < 30)) {
		if (.@mob_dead_num > 0) {
			areamonster .@map$,168,10,184,26,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead";
			if (rand(1,15) == 3) {
				mapannounce .@map$, "คำเตือน: Orc Archer กำลังเข้าใกล้แท่นบูชา.",bc_map,"0xff4444";
			}
		}
	}
	else {
		if (.@mob_dead_num > 0) {
			areamonster .@map$,168,21,184,21,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead";
		}
	}
	end;

OnDisable:
	killmonster instance_mapname("2@orcs"),instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead";
	end;
}

2@orcs,180,30,0	script	#2Resurrect Monsters4	844,{
	end;

OnInstanceInit:
	enablenpc instance_npcname("#2Resurrect Monsters4");
	monster instance_mapname("2@orcs"),0,0,"Anopheles",1627,10,instance_npcname("#2Resurrect Monsters4")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("2@orcs");
	set .@mob_dead_num, 10 - mobcount(.@map$,instance_npcname("#2Resurrect Monsters4")+"::OnMyMobDead");
	if (.@mob_dead_num > 0) {
		monster .@map$,0,0,"Anopheles",1627,1,instance_npcname("#2Resurrect Monsters4")+"::OnMyMobDead";
	}
	end;
}

2@orcs,35,169,4	script	Kruger#2-1	433,{
	donpcevent instance_npcname("Kruger#2-2")+"::OnEnable";
	end;
}

2@orcs,35,169,4	script	Kruger#2-2	433,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("Kruger#2-2");
	end;

OnEnable:
	disablenpc instance_npcname("Kruger#2-1");
	enablenpc instance_npcname("Kruger#2-2");
	initnpctimer;
	end;

OnTimer10:
	mapannounce instance_mapname("2@orcs"), "เสียงกระซิบของ Kruger: ข้าจะบอกวิธีไปยังแท่นบูชาของ Shaman ให้",bc_map,"0xffff00";
	end;

OnTimer3510:
	mapannounce instance_mapname("2@orcs"), "เสียงกระซิบของ Kruger: เจ้าเห็นจุดต่างๆที่ Shaman สร้างไว้หลายแห่งไหม? เจ้าจะผ่านไปยัง zone ต่อไปได้ ถ้าเจ้าจุดเตานั้นด้วยไฟที่แรงพอ",bc_map,"0xffff00";
	end;

OnTimer10710:
	mapannounce instance_mapname("2@orcs"), "เสียงกระซิบของ Kruger: แน่นอนว่าพวกมอนเตอรืไม่มีทางปล่อยให้เจ้าไปแตะมันได้ง่ายๆ ข้าหมายถึง..การต่อสู้ที่หลีกเลี่ยงไม่ได้",bc_map,"0xffff00";
	end;

OnTimer16310:
	mapannounce instance_mapname("2@orcs"), "เสียงกระซิบของ Kruger: และเจ้ายังต้องสู้ต่อไปโดยไม่ให้ผิดสังเกตุเพื่อไม่ให้ Shaman ส่งทหารมาป้องกันแท่นบูชา",bc_map,"0xffff00";
	end;

OnTimer21910:
	mapannounce instance_mapname("2@orcs"), "เสียงกระซิบของ Kruger: เฉพาะหัวหน้าปาตี้เท่านั้นที่สามารถจุดไฟได้, ดังนั้นปกป้องหัวหน้าปาตี้เจ้าซะ",bc_map,"0xffff00";
	end;

OnTimer23910:
	mapannounce instance_mapname("2@orcs"), "ภารกิจ: ปลดผนึกโซนด้วยเตาไฟที่ร้อนแรง ต่อสู้ให้น้อยที่สุด เตาไฟจะต้องถูกจุดตามลำดับ และทั้งหมดไม่ใช่จะจุดติดได้",bc_map,"0x4444ff";
	donpcevent instance_npcname("#2Resurrect Monsters1")+"::OnEnable";
	donpcevent instance_npcname("#2Resurrect Monsters3")+"::OnEnable";
	donpcevent instance_npcname("Torch#1-1")+"::OnEnable";
	disablenpc instance_npcname("Kruger#2-2");
	end;
}

2@orcs,26,164,0	script	Torch#1-1	844,{
	if (is_party_leader() == false) end;
	progressbar "ffff00",5;
	setarray .@id[0], atoi(charat(strnpcinfo(2),0)), atoi(charat(strnpcinfo(2),2));
	if (.@id[1] == 4)
		donpcevent instance_npcname("#Warp2-"+.@id[0])+"::OnEnable";
	else
		donpcevent instance_npcname("Torch#"+.@id[0]+"-"+(.@id[1]+1))+"::OnEnable";
	initnpctimer;
	disablenpc();
	end;

OnInstanceInit:
	if (strnpcinfo(0) != "Torch#2-1" && strnpcinfo(0) != "Torch#3-1")
		disablenpc();
	end;

OnEnable:
	enablenpc();
	end;

OnTimer100:
	specialeffect EF_FIREPILLAR;
	end;

OnTimer2000:
	specialeffect EF_FIREPILLARBOMB;
	stopnpctimer;
	end;
}
2@orcs,55,155,0	duplicate(Torch#1-1)	Torch#1-2	844
2@orcs,108,146,0	duplicate(Torch#1-1)	Torch#1-3	844
2@orcs,98,171,0	duplicate(Torch#1-1)	Torch#1-4	844

2@orcs,48,100,0	script	#Warp2-1	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("#Warp2-1");
	end;

OnEnable:
	set .@map$, instance_mapname("2@orcs");
	monster .@map$,109,156,"Safeguard Chief",1981,1,instance_npcname("#Mobs Control")+"::OnMyMobDead1";
	mapannounce .@map$, "The Chief Orc of Safeguards: โอ้!! ดูเหมือนจะมีผู้ท้าทาย แน่จริงก็ล้มข้าให้ได้สิ!!",bc_map,"0xff8888";
	end;

OnContinue:
	enablenpc instance_npcname("#Warp2-1");
	initnpctimer;
	end;

OnTimer10000:
	areamonster instance_mapname("2@orcs"),28,158,40,170,"Safeguard Chief",1981,1,instance_npcname("#Mobs Control")+"::OnMyMobDead1";
	stopnpctimer;
	end;

OnTouch_:
	warp instance_mapname("2@orcs"),47,93;
	end;
}

2@orcs,35,92,0	duplicate(Torch#1-1)	Torch#2-1	844
2@orcs,32,70,0	duplicate(Torch#1-1)	Torch#2-2	844
2@orcs,70,31,0	duplicate(Torch#1-1)	Torch#2-3	844
2@orcs,84,51,0	duplicate(Torch#1-1)	Torch#2-4	844

2@orcs,101,55,0	script	#Warp2-2	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("#Warp2-2");
	end;

OnEnable:
	set .@map$, instance_mapname("2@orcs");
	monster .@map$,67,64,"Orc Sniper",1982,1,instance_npcname("#Mobs Control")+"::OnMyMobDead2";
	mapannounce .@map$, "Orc Sniper: เก่งมาก! ประทับใจมากที่มาถึงนี่ได้ แต่การเดินทางของเจ้าคงจบลงที่นี่แล้ว...",bc_map,"0xff8888";
	end;

OnContinue:
	enablenpc instance_npcname("#Warp2-2");
	initnpctimer;
	end;

OnTimer10000:
	areamonster instance_mapname("2@orcs"),40,91,52,103,"Orc Sniper",1982,1,instance_npcname("#Mobs Control")+"::OnMyMobDead2";
	stopnpctimer;
	end;

OnTouch_:
	warp instance_mapname("2@orcs"),107,55;
	end;
}

2@orcs,142,145,0	duplicate(Torch#1-1)	Torch#3-1	844
2@orcs,162,134,0	duplicate(Torch#1-1)	Torch#3-2	844
2@orcs,144,117,0	duplicate(Torch#1-1)	Torch#3-3	844
2@orcs,136,98,0	duplicate(Torch#1-1)	Torch#3-4	844

2@orcs,167,104,0	script	#Warp2-3	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("#Warp2-3");
	end;

OnEnable:
	set .@map$, instance_mapname("2@orcs");
	monster .@map$,152,147,"Depraved Orc Spirit",1983,1,instance_npcname("#Mobs Control")+"::OnMyMobDead3";
	mapannounce .@map$, "Depraved Orc Spirit: ข้าได้กลิ่นเนื้อสด หิว! อยากจะลองเนื้อมนุษย์ฉ่ำ ๆ !!",bc_map,"0xff8888";
	end;

OnContinue:
	areamonster instance_mapname("2@orcs"),117,61,129,73,"Depraved Orc Spirit",1983,1;
	donpcevent instance_npcname("#Boss Control")+"::OnEnable";
	enablenpc instance_npcname("#Warp2-3");
	initnpctimer;
	end;

OnTimer10:
	mapannounce instance_mapname("2@orcs"), "Shaman Cargalache: เจ้าเก่งมากเลยนะ!! ในที่สุดก็มาถึงที่นี่ได้ นักฆ่าที่เจ้าส่งมานี่ช่างน่ากลัวจริงๆ orc นั่นหมอบแทบเท้าข้าไปซะแล้ว",bc_map,"0xffff00";
	end;

OnTimer6810:
	mapannounce instance_mapname("2@orcs"), "Shaman Cargalache: สมุนที่พักดีของข้า สั่งสอนมัน!",bc_map,"0xffff00";
	end;

OnTimer10310:
	mapannounce instance_mapname("2@orcs"), "Depraved Orc Hero: ได้เลย นายท่าน",bc_map,"0xff7777";
	end;

OnTimer13110:
	mapannounce instance_mapname("2@orcs"), "ระวัง: เจ้าถูกจับได้โดย Shaman Cargalache แผนของ Kruger ที่จะลอบสังหารล้มเหลว เจ้าต้องกำจัด Cargalache และหาร่องรอยของ Kruger.",bc_map,"0x8888ff";
	stopnpctimer;
	end;

OnTouch_:
	warp instance_mapname("2@orcs"),167,95;
	end;
}

2@orcs,36,171,0	script	#Boss Control	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#Boss Control");
	end;

OnEnable:
	set .@map$, instance_mapname("2@orcs");
	monster .@map$,185,8,"Shaman Cargalache",1984,1,instance_npcname("#Boss Control")+"::OnMyMobDead";
	monster .@map$,179,15,"Depraved Orc Hero",1087,1;
	enablenpc instance_npcname("#Boss Control");
	end;

OnMyMobDead:
	donpcevent instance_npcname("Kruger#")+"::OnEnable";
	set .@map$, instance_mapname("2@orcs");
	set .@mob_ran,rand(1,5);
	if (.@mob_ran == 1) {
		mapannounce .@map$, "Shaman Cargalache: ข้า... ข้าไม่อยากเชื่อเลย... เจ้ากล้าดียังไง...!!",bc_map,"0xffff00";
	}
	else if (.@mob_ran == 2) {
		mapannounce .@map$, "Shaman Cargalache: เป็นไปได้ยังไง มนุษย์แข็งแกร่งขนาดนี้ได้ยังไงกัน!",bc_map,"0xffff00";
	}
	else if (.@mob_ran == 3) {
		mapannounce .@map$, "Shaman Cargalache: ไม่... สิ่งนี้มันยังไม่จบ...",bc_map,"0xffff00";
	}
	else if (.@mob_ran == 4) {
		mapannounce .@map$, "Shaman Cargalache: ฉัน... ยังไม่ตาย... หรอก...!",bc_map,"0xffff00";
	}
	else {
		mapannounce .@map$, "Shaman Cargalache: พ่ายแพ้เจ้าคนโง่เนี่ยนะ... มันไม่น่าจะเกิดขึ้นได้...!",bc_map,"0xffff00";
	}
	donpcevent instance_npcname("#2Resurrect Monsters1")+"::OnDisable";
	donpcevent instance_npcname("#2Resurrect Monsters3")+"::OnDisable";
	donpcevent instance_npcname("#Warp Outside Orc Dun")+"::OnEnable";
	end;
}

2@orcs,182,8,0	script	#Warp Outside Orc Dun	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("#Warp Outside Orc Dun");
	end;

OnEnable:
	enablenpc instance_npcname("#Warp Outside Orc Dun");
	end;

OnTouch_:
	warp "gef_fild10",240,197;
	end;
}

2@orcs,172,13,0	script	Kruger#	434,{
	if (yong_odun < 2) {
		mes "[Kruger]";
		mes "*อ่อกแอ่ก*";
		mes ""+strcharinfo(0)+", เจ้าเองหรอ...";
		next;
		mes "["+strcharinfo(0)+"] ";
		mes "อย่าขยับตัวมาก! เจ้าได้รับบาดเจ็บ!";
		next;
		mes "[Kruger]";
		mes "ไม่... เป็นไร.. ข้ากำลังจะตาย...";
		mes "....";
		mes "The Shaman? เกิดอะไรขึ้นกับ Shaman?";
		next;
		mes "["+strcharinfo(0)+"] ";
		mes "The Shaman's ตายแล้ว. จริงๆแล้ว Shaman เป็นใครกันแน่?";
		next;
		mes "ดูเหมือนจะหายห่วงที่ได้ยินว่า Shaman ตายแล้ว, แต่คุณสังเกตุเห็นเขายังกังวลบางอย่างอยู่";
		next;
		mes "[Kruger]";
		mes "ข้า.. ฆ่าลูกสาวของข้าไม่ลง...";
		mes "ขอบใจเจ้ามาก, ข้ามั่นใจว่าในที่สุดเธอก็หลุดพ้นจาก ฝันร้ายที่หลอกหลอนเธอมาตลอด";
		next;
		mes "Kruger เหมือนจะพูดอะไรมากกว่านี้, แต่เขาสิ้นใจก่อนที่จะได้พูดออกมา...";
		set yong_odun,2;
		close;
	}
	else {
		mes "คุณเห็นร่างของ Kruger สิ้นแรง, แล้วนอนสิ้นล้มลงไปต่อหน้า.";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("Kruger#");
	end;

OnEnable:
	enablenpc instance_npcname("Kruger#");
	end;
}

2@orcs,36,171,0	script	#Mobs Control	844,{
	end;

OnMyMobDead1:
	donpcevent instance_npcname("#Warp2-1")+"::OnContinue";
	end;

OnMyMobDead2:
	donpcevent instance_npcname("#Warp2-2")+"::OnContinue";
	end;

OnMyMobDead3:
	donpcevent instance_npcname("#Warp2-3")+"::OnContinue";
	end;
}
